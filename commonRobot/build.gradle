apply plugin: 'cpp'
apply plugin: 'google-test-test-suite'
apply plugin: 'edu.wpi.first.GradleRIO'

model {
  components {
    curtinFrcCommonRobot(NativeLibrarySpec) {
      targetPlatform wpi.platforms.desktop
      targetPlatform wpi.platforms.roborio

      binaries.all {
        lib project: ':common', library: 'curtinFrcCommon', linkage: 'static'

        if (targetPlatform.name == wpi.platforms.roborio) {
          sources {
            robotCpp(CppSourceSet) {
              source {
                srcDir 'src/main/cpp'
                srcDir 'src/main/robot'
              }
              exportedHeaders {
                srcDir 'src/main/include'
              }
            }
          }
        } else {
          sources {
            simCpp(CppSourceSet) {
              source {
                srcDir 'src/main/cpp'
                srcDir 'src/main/sim'
              }
              exportedHeaders {
                srcDir 'src/main/include'
              }
            }
          }
        }
      }

      useLibrary(it, 'wpilib')
    }
  }

  testSuites {
    curtinFrcCommonRobotTest(GoogleTestTestSuiteSpec) {
      testing $.components.curtinFrcCommonRobot

      sources.cpp {
        source {
          srcDir 'src/test/cpp'
        }
      }

      binaries.all {
        lib project: ':common', library: 'curtinFrcCommon', linkage: 'static'
      }

      useLibrary(it, 'wpilib', 'googletest')
    }
  }
}